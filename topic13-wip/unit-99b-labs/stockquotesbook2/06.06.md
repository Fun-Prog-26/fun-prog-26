#### Generating a HTML document - HTMLReport.hs

In generating our HTML code and eventually generating a html file containing that code, we lean on the structures built in StatReport (using Colonnade) and, in addition, we use html specific libraries: 

- **Text.Blaze.Html5**

- **Text.Blaze.Html**

  

We wish to generate code looking like so: 

```html
<html>
  <head>
    <title>...</title>
    <style>...</style>
  </head>
  <body>
    <h1>Charts</h1>
    <!-- charts -->
    <h1>Statistics Report</h1>
    <!-- statistics table -->
    <h1>Stock Quotes Data</h1>
    <!-- quote data table -->
  </body>
</html>
```



To build this code, we need the two following packages : 

```haskell
import Text.Blaze.Html5 as H
import Text.Blaze.Html5.Attributes (src)
```



So if we start looking at tables, we need a function: 

```haskell
viaFmt :: Buildable a => a -> Html
viaFmt = text . pretty
```

The `text` function expects `Text` for input, and it outputs an `Html` value. Consequently, the `pretty` function from `fmt` will provide `Text` from the given `Buildable`. 

Next, we describe the tables that we wish to generate:

```haskell
colStats :: Colonnade Headed StatEntry Html
colStats = mconcat
      [ headed "Quote Field" (i . string . show . qfield)
      , headed "Mean" (viaFmt . meanVal)
      , headed "Min" (viaFmt . minVal)
      , headed "Max" (viaFmt . maxVal)
      , headed "Days between Min/Max" (viaFmt . daysBetweenMinMax)
      ]

colData :: Colonnade Headed QuoteData Html
colData = mconcat
      [ headed "Day" (viaFmt . day)
      , headed "Open" (viaFmt . showPrice . open)
      , headed "Close" (viaFmt . showPrice . close)
      , headed "High" (viaFmt . showPrice . high)
      , headed "Low" (viaFmt . showPrice . low)
      , headed "Volume" (viaFmt . volume)
      ]
```



The `Text.Blaze.Colonnade` module provides two useful functions to look at these table structures in action. The `encodeHtmlTable` function takes `Attributes` for the table (it’s a `Monoid` and we can use `mempty` for no-attributes), table structure, and a list of raw data. It returns an `Html` value, that can be printed via the `printCompactHtml` function. We can look at the HTML code generated with the table structures defined above as follows:

```html
ghci> quotes <- readQuotes "data/quotes.csv"
ghci> si = statInfo quotes
ghci> import Text.Blaze.Colonnade
ghci> printCompactHtml (encodeHtmlTable mempty colStats si)
<table>
    <thead>
        <tr>
            <th>QuoteField</th>
            <th>Mean</th>
            <th>Min</th>
            <th>Max</th>
            <th>DaysbetweenMin/Max</th>
        </tr>
    </thead>
    <tbody>
     ...
    </tbody>
</table>
```

(we are using printCompactHtml just for checking - we won't use it in the code)

So the rest of the code: 

```haskell
htmlReport :: (Functor t, Foldable t) =>
             t QuoteData -> [StatEntry]  -> ByteString
htmlReport quotes statEntries  = renderHtml $ docTypeHtml $ do
     H.head $ do
       style tableStyle

     body $ do
      
       h1 "Statistics Report"
       encodeHtmlTable mempty colStats statEntries

       h1 "Stock Quotes Data"
       encodeHtmlTable mempty colData quotes
  where
    tableStyle = "table {border-collapse: collapse}" <>
            "td, th {border: 1px solid black; padding: 5px}"

```


Look at 

```haskell
tableStyle = "table {border-collapse: collapse}" <>
            "td, th {border: 1px solid black; padding: 3px}"
```

the following style definition for HTML tables with the `<>` semigroup operation:

The `blaze-html` package relies heavily on the `OverloadedStrings` GHC extension to turn every `String` literal into a value of the `Html` type.

The `Html` we’ve got is rendered as a `ByteString`  so that we can export to a file. Note that the `htmlReport` is a pure function. We don’t have `IO` in its type. Everything in this module works in a pure part of our program.

The full code: 

```haskell
{-# LANGUAGE OverloadedStrings #-}

module HtmlReport where

import Data.Foldable (traverse_)
import Control.Monad (unless)
import Data.ByteString.Lazy (ByteString)
import Text.Blaze.Html5 as H
import Text.Blaze.Html5.Attributes (src)
import Text.Blaze.Html.Renderer.Utf8 (renderHtml)
import Colonnade
import Text.Blaze.Colonnade
import Fmt (pretty, Buildable)

import QuoteData
import StatReport

viaFmt :: Buildable a => a -> Html
viaFmt = text . pretty

colStats :: Colonnade Headed StatEntry Html
colStats = mconcat
      [ headed "Quote Field" (i . string . show . qfield)
      , headed "Mean" (viaFmt . meanVal)
      , headed "Min" (viaFmt . minVal)
      , headed "Max" (viaFmt . maxVal)
      , headed "Days between Min/Max" (viaFmt . daysBetweenMinMax)
      ]

colData :: Colonnade Headed QuoteData Html
colData = mconcat
      [ headed "Day" (viaFmt . day)
      , headed "Open" (viaFmt . showPrice . open)
      , headed "Close" (viaFmt . showPrice . close)
      , headed "High" (viaFmt . showPrice . high)
      , headed "Low" (viaFmt . showPrice . low)
      , headed "Volume" (viaFmt . volume)
      ]

htmlReport :: (Functor t, Foldable t) =>
             t QuoteData -> [StatEntry]  -> ByteString
htmlReport quotes statEntries  = renderHtml $ docTypeHtml $ do
     H.head $ do
       style tableStyle

     body $ do
      
       h1 "Statistics Report"
       encodeHtmlTable mempty colStats statEntries

       h1 "Stock Quotes Data"
       encodeHtmlTable mempty colData quotes
  where
    tableStyle = "table {border-collapse: collapse}" <>
            "td, th {border: 1px solid black; padding: 5px}"


```